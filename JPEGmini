#!/bin/osascript

on guiScriptIsEnabled()
  tell application "System Events"
    set isEnabled to UI elements enabled
  end tell
  if isEnabled then
    return 1
  else
    return 0
  end if
end guiScriptIsEnabled

on promptToEnableGuiScript()
  tell application "System Preferences"
    activate
    set current pane to pane id "com.apple.preference.universalaccess"
  end tell
end promptToEnableGuiScript

on optimise_directory(dirPath)
  try
    tell application "JPEGmini"
      activate
      delay 2
      activate
    end tell
    tell application "System Events"
      tell process "JPEGmini"
        click menu item "Openâ€¦" of menu 1 of menu bar item "File" of menu bar 1
        keystroke "g" using {command down, shift down}
        keystroke dirPath
        keystroke return
        delay 1
        click button "Open" of sheet 1 of window "JPEGmini"
        return 1
      end tell
    end tell
    return 1
    on error error_message
      return 0
  end try
end optimise_directory

on waitForApp()
  tell application "System Events"
    repeat
      set PID to unix id of process "JPEGmini"
      set getCpuPercent to "ps aux | grep " & PID & " | grep -v grep | awk '{print $3}'"
      set cpuPercent to (do shell script getCpuPercent) as number
      log cpuPercent as string
      if (cpuPercent) < 1 then
        exit repeat
      end if
      delay 2
    end repeat
  end tell
end waitForApp

on run argv
  set command to item 1 of argv
  if command is "check" then
    return guiScriptIsEnabled()
  else if command is "prompt" then
    promptToEnableGuiScript()
  else if command is "run" then
    set dirPath to item 2 of argv
    optimise_directory(dirPath)
    waitForApp()
  end if
end run
